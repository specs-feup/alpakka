plugins {
    id 'distribution'
    id 'java' // Java project
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

// Repositories providers
repositories {
    // Gearman
    maven { url "https://oss.sonatype.org/content/repositories/snapshots"}

    // smali
    google()

    mavenCentral()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {
    testImplementation "junit:junit:4.13.1"

    implementation ":SpecsUtils"
    implementation ":jOptions"
    
    implementation ":LARAI"
    implementation ":LaraUtils"
    implementation ":WeaverInterface"
    implementation ":LanguageSpecification"

    implementation ":AlpakkaParser"
    implementation ":AlpakkaAst"
    implementation ":AlpakkaLaraApi"

    weaverGeneratorRuntime ":WeaverGenerator"
}


// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDir 'resources'
        }      
    }

    test {
        java {
            srcDir 'test'
        }

        resources {
            srcDir 'resources'
            srcDir 'test-resources'
        }
    }
}

// Weaver Generator
task generateWeaver(type: JavaExec) {
    group = "Execution"
    description = "Generates the Weaver Abstracts from the Language Specification"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'
    args = [
            '-w', 'SmaliWeaver',
            '-x', './resources/smali/weaverspecs',
            '-o', './src',
            '-p', 'pt.up.fe.specs.alpakka.weaver',
            '-n', 'pt.up.fe.specs.alpakka.ast.SmaliNode',
            '-j',
            '-e',
            '-d'
    ]
}

compileJava.dependsOn generateWeaver
